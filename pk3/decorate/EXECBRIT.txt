// Sound emitters which replace the ambient monster sounds in Execution Deathmatch and Brit11

Actor LexiconDM_BREX_Fire2SoundEmitter : LexiconAmbientSoundEmitter
{
	States
	{
		Spawn:
			TNT1 A 2
			TNT1 A 0 A_Jump(3, "MakeSound")
			Loop
		MakeSound:
			TNT1 A 204 A_PlaySound("BREX/FIRE2", CHAN_VOICE, 1.0, false, ATTN_IDLE)
			Goto Spawn
	}
}

Actor LexiconDM_BREX_HowlingWindSoundEmitter : LexiconAmbientSoundEmitter
{
	States
	{
		Spawn:
			TNT1 A 3
			TNT1 A 0 A_Jump(3, "MakeSound")
			Loop
		MakeSound:
			TNT1 A 281 A_PlaySound("BREX/HOWLINGWIND", CHAN_VOICE, 1.0, false, ATTN_IDLE)
			Goto Spawn
	}
}

Actor LexiconDM_BREX_ColdWindSoundEmitter : LexiconAmbientSoundEmitter
{
	States
	{
		Spawn:
			TNT1 A 4
			TNT1 A 0 A_Jump(3, "MakeSound")
			Loop
		MakeSound:
			TNT1 A 334 A_PlaySound("BREX/COLDWIND", CHAN_VOICE, 1.0, false, ATTN_IDLE)
			Goto Spawn
	}
}

Actor LexiconDM_BREX_MoanSoundEmitter : LexiconDM_BREX_Fire2SoundEmitter
{
	States
	{
		MakeSound:
			TNT1 A 114 A_PlaySound("BREX/MOAN", CHAN_VOICE, 1.0, false, ATTN_IDLE)
			Goto Super::Spawn
	}
}

Actor LexiconDM_BREX_LightningSoundEmitter : LexiconDM_BREX_HowlingWindSoundEmitter
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 222 A_PlaySound("BREX/LIGHTNING", CHAN_VOICE, 1.0, false, ATTN_NONE)
			Goto Super::Spawn
		MakeSound:
			TNT1 A 203 A_PlaySound("BREX/FIRE2", CHAN_VOICE, 1.0, false, ATTN_IDLE)
			Goto Super::Spawn
	}
}

Actor LexiconDM_BREX_HowlingWind2SoundEmitter : LexiconDM_BREX_ColdWindSoundEmitter
{
	States
	{
		MakeSound:
			TNT1 A 166 A_PlaySound("DECA/WIND2", CHAN_VOICE, 1.0, false, ATTN_IDLE)
			Goto Super::Spawn
	}
}

Actor LexiconDM_BREX_FireSoundEmitter : LexiconDM_BREX_HowlingWindSoundEmitter
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Jump(127, 1)
			Goto Super::Spawn
			TNT1 A 29 A_PlaySound("DECA/WIND1START", CHAN_VOICE, 1.0, false, ATTN_NORM)
			Goto Super::Spawn
		MakeSound:
			TNT1 A 148 A_PlaySound("BREX/FIRE", CHAN_VOICE, 1.0, false, ATTN_IDLE)
			Goto Super::Spawn
	}
}

Actor LexiconDM_BREX_WaterSoundEmitter : LexiconDM_BREX_Fire2SoundEmitter
{
	States
	{
		MakeSound:
			TNT1 A 173 A_PlaySound("DECA/WATER2", CHAN_VOICE, 1.0, false, ATTN_IDLE)
			Goto Super::Spawn
	}
}

Actor LexiconDM_BREX_ScreamSoundEmitter : LexiconAmbientSoundEmitter
{
	Speed 0
	SeeSound "BREX/SCREAM"
	ActiveSound "DECA/WATER2"
	States
	{
		Spawn:
			TNT1 A 10 A_LookEx(LOF_NOSOUNDCHECK, 0, 0, 0, 360)
			Loop
		See:
			TNT1 A 3 A_Chase("", "", CHF_DONTMOVE)
			Loop
	}
}

// Actors for the scripted sequence at the end of BREX03
Actor LexiconDM_BREX_FakeZombieman
{
	Health 20
	Radius 20
	Height 56
    +CANPASS
	+DONTGIB
	+FLOORCLIP
	+NEVERRESPAWN
    +SHOOTABLE
    +SOLID
	States
	{
		Spawn:
			POSS AB 10
			Loop
		Death:
			POSS M 5
			POSS N 5 A_PlaySound("BREX/GIBBED", CHAN_VOICE, 1.0, false, ATTN_NORM)
			POSS O 5 A_NoBlocking
			POSS PQRST 5
			POSS U -1
			Stop
	}
}

Actor LexiconDM_BREX_FakeWolfensteinSS : LexiconDM_BREX_FakeZombieman
{
	Health 50
	States
	{
		Spawn:
			SSWV AB 10
			Loop
		Death:
			SSWV I 5
			SSWV J 5 A_PlaySound("BREX/SAWDEATH", CHAN_VOICE, 1.0, false, ATTN_NORM)
			SSWV K 5 A_NoBlocking
			SSWV L 5
			SSWV M -1
			Stop
	}
}

Actor LexiconDM_BREX_NonRespawningExplosiveBarrel : ExplosiveBarrel
{
	States
	{
		Death:
			BEXP A 5 Bright
			BEXP B 5 Bright A_Scream
			BEXP C 5 Bright
			BEXP D 5 Bright A_Explode
			BEXP E 10 Bright
			Stop
	}
}
